**
** MQTT Worker Base Class
**
@palette=false
abstract internal class Worker extends Component
{
  ////////////////////////////////////////////////////////////////
  // Properties
  ////////////////////////////////////////////////////////////////
  @config @asStr property Buf(64) host
  @config property int port = 1883

  @config @asStr property Buf(16) clientid
  @config @asStr property Buf(16) username
  @config @asStr property Buf(16) password

  define int Ok = 0
  define int Err_InvalidParent = 1
  define int Err_InvalidHost = 2
  define int Err_InvalidPort = 3
  define int Err_InvalidClientId = 4

  define Str WorkerStatus = "Ok, Invaid Parent, Invalid Host, Invalid Port, Invalid ClientId"
  @range=WorkerStatus
  @readonly property byte status = 0
  
  ////////////////////////////////////////////////////////////////
  // Methods
  ////////////////////////////////////////////////////////////////
  abstract void doWork(Message m) 
  
  // natives 
  static native Obj startSession(Str host, int port, Str clientid, Str username, Str password)
  static native void stopSession(Obj handle)
  static native bool isSessionLive(Obj handle)

  virtual void onStatusOk() {
    if (handle != null)
      return 

    handle = startSession(host.toStr(), port, clientid.toStr(), username.toStr(), password.toStr())
  }
  
  void validateStatus() 
  {
    Component parentComponent = Sys.app.lookup(parent)
    if (!parentComponent.type.is(MQTTService.type))
      status := Err_InvalidParent
    else if (port == 0)
      status := Err_InvalidPort
    else if (host.toStr().length() == 0)
      status := Err_InvalidHost
    else if (clientid.toStr().length() == 0)
      status := Err_InvalidClientId
    else
      status := Ok

    if (status == Ok)
      onStatusOk()
  }

  override virtual void start() 
  {
    validateStatus()
  }
  
  override virtual void stop() 
  {
    if (handle == null)
      return

    stopSession(handle)
    handle = null
    resetAllChildren()
  }
  
  void resetAllChildren()
  {
    for(Component child = Sys.app.lookup(children); child != null; child = Sys.app.lookup(child.nextSibling))
    {
      if (child.type.is(Message.type)) {
        Message m = (Message)child
        m.reset()
      }
    }
  }

  override int parentEvent(int eType, Component parent)
  {
    if (eType == Component.ADDED && parent.type.is(MQTTService.type))
      status := Err_InvalidParent; 
    return 0
  }
  
  virtual override void changed(Slot slot) 
  {
    super.changed(slot)
    int sid = slot.id
    if (sid == Worker.host.id || sid == Worker.port.id || sid == Worker.clientid.id)
      validateStatus()
  }
  
  virtual bool work()
  {
    if (status != Ok || handle == null || children == nullId)
      return false

    if (!isSessionLive(handle))
      return false
        
    for(Component child = Sys.app.lookup(children); child != null; child = Sys.app.lookup(child.nextSibling))
    {
      if (child.type.is(Message.type)) {
        Message m = (Message)child
        if (m.status == Ok)
          doWork(m)
      }
    }
    
    return true
  }

  ////////////////////////////////////////////////////////////////
  // Fields
  ////////////////////////////////////////////////////////////////
  // SessionHandle 
  protected Obj handle = null
  
  internal define Log log
}
